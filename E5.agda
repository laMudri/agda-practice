-- First steps in Agda: more on logic.

module E5 where

  -- This will be a long ramble as I try to firm up some things that have been left rather vague
  -- until nowâ€¦

  -- Until now, we have been fairly hand-wavy about how we can encode logical connectives
  -- and quantifiers in Agda's type theory, in order to build some intuition without
  -- pushing the theory too hard.  Indeed, you may have noticed that some familiar aspects
  -- of logic have been elided altogether (for instance the existential quantifier).
  --
  -- Further, we've also been hand-wavy about what exactly you are doing when you are proving
  -- some theorem in Agda.  Let's first discuss that.  Agda is at heart, a dependent type
  -- theory.  You may have noticed (or been bludgeoned over the head with the fact) that types
  -- are rather important to Agda --- `everything' has a type, and Agda is only too keen to
  -- declare that some term is ill-typed if you make a mistake.  Simplified, Agda's type-checking
  -- algorithm is based around a typing relation between contexts (lists of variables with an
  -- assigned type), terms (for example, `Î»x. x', `true', `Set', and so forth) and types (`Set',
  -- `Bool â†’ Bool', `â„•', and so forth).  You may note with interest that some terms are also
  -- types (for instance, `Set').  We write `Î“ âŠ¢ M : Ï†' (where Î“ is ranging over contexts,
  -- M is ranging over terms, and Ï† over types) for `Agda's typing relation asserts that term
  -- M has type Ï† in context Î“'.  The type checking algorithm then merely checks that for some
  -- term M, context Î“, and type Ï†, that `Î“ âŠ¢ M : Ï†' holds, raising a type error if it does
  -- not.  I won't go into the details of this typing relation (the rules are not entirely
  -- well-specified by the Agda implementors, for a start), but there's some fairly obvious
  -- rules that Agda implements below, to give you an idea:
  --
  -- How to type a variable:
  --
  --   (lookup x Î“ = Ï†)
  --  ------------------
  --      Î“ âŠ¢ x : Ï†
  --
  -- How to type Setáµ¢:
  --
  --      j = i + 1
  --  ------------------        so that Setâ‚ : Setâ‚‚ : Setâ‚ƒ : Setâ‚„ : â€¦
  --    Î“ âŠ¢ Setáµ¢ : Setâ±¼
  --
  -- How to type a Î»-abstraction:
  --
  --      Î“, x : Ï† âŠ¢ M : Ïˆ
  --  -------------------------
  --    Î“ âŠ¢ Î»x : Ï†. M : Ï† â†’ Ïˆ
  --
  -- plus dozens more complex typing rulesâ€¦
  --
  -- We call a term a type if it appears to the right hand side of the colon in the rules above.  As
  -- mentioned above, Agda makes no clear distinction between terms and types, other than via usage
  -- and the rules of the typing relation, and indeed it's perfectly possible in Agda to construct
  -- functions that take types as arguments (as you have already seen), and also produce types as
  -- results (you haven't already seen this, but soon will).
  --
  -- Anyway, if you aren't familiar with these sorts of rules, then you can read them as stating
  -- `assuming the hypotheses above the line hold, we can obtain the conclusion below by applying the
  -- rule'.  They're basically instructions for creating trees, but if they're confusing to you
  -- don't worry, you'll learn more about them in the Part II course on Types.
  --
  -- So what is it that you are doing when you are filling in holes in Agda?  Well, first
  -- note that every hole has a type and a context in which it lives.  If you cast your mind back to
  -- what an Agda proof state looked like, it was something of the form:
  --
  -- Goal: Ï†
  -- ---------------------------
  -- x : Ïˆ
  -- y : Î¾
  -- Ïˆ : Set
  -- Î¾ : Set
  --
  -- Here we can now make a connection between a proof state and the typing relation:
  --
  --     Ïˆ : Set, Î¾ : Set, x : Ïˆ, y : Î¾ âŠ¢ <?â‚> : Ï†
  --
  -- Where <?â‚> is the hole, appearing in place of a term.  `Filling in a hole' is therefore providing a
  -- term to stand in place of that <?â‚> that satisfies Agda's typing relation (i.e. that has type Ï† in
  -- the context above).  That is, we say we must find some M to `inhabit' the type Ï†.
  --
  -- But what about `logic'?  I've only been talking about types, not logic!  Well, in Agda and other
  -- similar systems, there is a close connection between the two.  Propositions, for example
  --
  --         âˆ€x. P x    âˆƒ!x. P x â‡’ Q x     False âˆ¨ (âˆ€x. âˆƒy. P x y â‡’ False)    â€¦
  --
  -- can be encoded as types.  We have already seen how some of these may be encoded in our informal
  -- introduction to Agda in exercises 1-4, but now we make these connections explicit, as well as what
  -- what constitutes a proof of, say P âˆ¨ Q, in the following table:
  --
  --   |  Proposition  |  Agda type        |  Inhabitant       |  Notes                             |
  --   +---------------+-------------------+-------------------+------------------------------------+
  --   |    True       |  Unit type  (ğŸ™)   |  Unit value (it)  |                                    |
  --   |    False      |  Empty type (ğŸ˜)   |  N/A              |  No inhabitant!                    |
  --   |    Ï† â‡’ Ïˆ      |  Ï† â†’ Ïˆ            |  Î»-abstraction    |                                    |
  --   |    Â¬ Ï†        |  Ï† â†’ ğŸ˜            |  Î»-abstraction    |  Function must return ğŸ˜            |
  --   |    âˆ€x. P x    |  (x : Set) â†’ P x  |  Î»-abstraction    |  âˆ€, â‡’ both encoded as function     |
  --   |               |                   |                   |  arrows                            |
  --   |    Ï† â€Œâˆ§ Ïˆ      |  Ï† Ã— Ïˆ            |  Pair             |                                    |
  --   |    Ï† âˆ¨ Ïˆ      |  Ï† âŠ Ïˆ            |  injâ‚ or injâ‚‚     |  Must explicitly show one or the   |
  --   |               |                   |                   |  other holds                       |
  --   |    Ï† = Ïˆ      |  Ï† â‰¡ Ïˆ            |  _â‰¡_, Identity    |  e.g. refl                         |
  --   |    âˆƒx. P x    |  Î£ Set (Î»x â†’ P x) |  Dependent-pair   |  See below                         |
  --   +---------------+-------------------+-------------------+------------------------------------+
  --
  -- The last line you have not encountered (yet), but will below.
  --
  -- Some observations about the table above now follow.  You'll note that ğŸ˜ is marked as having no
  -- inhabitant.  This is very important.  ğŸ˜ is the encoding of False, and finding an inhabitant of ğŸ˜ is
  -- akin to finding a proof of False, which we surely do not want.  You'll also note, however, that
  -- Â¬ Ï† is captured by the type Ï† â†’ ğŸ˜, which is inhabited by Î»(x : Ï†). M, where M is a term of type ğŸ˜,
  -- i.e. a proof of False.  But if we cannot inhabit ğŸ˜, how can we construct such an M?  Well, we have to
  -- make use of x : Ï† somehow in order to do it.  The restriction against finding a proof of False is
  -- only valid in the empty context.  If we are given some inconsistent assumptions, we can of course
  -- find such a proof.
  --
  -- You'll also note that Ï† â‡’ Ïˆ and âˆ€x. P x are both encoded as forms of function space, and both inhabited
  -- by Î»-abstractions, albeit the latter is encoded as the more general Î -type (dependent function space,
  -- (x : Set) â†’ â€¦).  As mentioned earlier, in Agda the types Ï† â†’ Ïˆ and (x : Ï†) â†’ Ïˆ are equivalent, with
  -- Ï† â†’ Ïˆ being merely a special case of the latter when x doesn't appear in Ïˆ.
  --
  -- Further, note that disjunction in Agda is something slightly different to what you are used to from
  -- classical mathematics.  In Agda, to prove a disjunction, Ï† âŠ Ïˆ, you must show that Ï† or Ïˆ holds
  -- explicitly, i.e. you must explcitly `construct' an inhabitant of Ï† or Ïˆ.  In classical mathematics,
  -- one may show that Ï† âˆ¨ Ïˆ holds by demonstrating that it is impossible for either not to hold.  Not so
  -- in Agda!
  --
  -- You'll note from the table above that proving anything in Agda requires one to explicitly construct
  -- something, whether it be an inhabitant of ğŸ™ to prove True, or a function that accepts a proof of Ï†
  -- and constructs a proof of ğŸ˜ to prove Â¬ Ï†.  In short, Agda's internal logic is constructive, and
  -- differs from classical mathematics.  An immediate consequence of working with constructive logic is
  -- that some familiar reasoning principles are no longer valid.  Try to prove the following, for
  -- example:

  -- Some familiar infrastructure without importing Exercise 1-4:

  data ğŸ˜ : Set where

  data _âŠ_ (A B : Set) : Set where
    injâ‚ : A â†’ A âŠ B
    injâ‚‚ : B â†’ A âŠ B

  Â¬_ : Set â†’ Set
  Â¬ Ï† = Ï† â†’ ğŸ˜

  double-negation-elimination : (Ï† : Set) â†’ Â¬ (Â¬ Ï†) â†’ Ï†
  double-negation-elimination Ï† = {!!} -- may take a while to close!

  -- This is valid classically (it's equivalent to the law of excluded middle), but cannot be proved in
  -- Agda's intuitionistic logic.  You can, however, prove the weaker

  triple-negation-to-single-negation : (Ï† : Set) â†’ Â¬ (Â¬ (Â¬ Ï†)) â†’ Â¬ Ï†
  triple-negation-to-single-negation Ï† prf = Î» x â†’ prf (Î» y â†’ y x)

  -- Ho hum, some things we normally take for granted we no longer can.  But in exchange for that, Agda's
  -- proofs are much more informative.  Merely by virtue of constructivity, if we manage to prove Ï†, we
  -- automatically get a program that inhabits Ï† for free.  That is, Agda's proofs, unlike proofs in
  -- general in classical mathematics, have some computational content.  This becomes especially interesting
  -- where Ï† is some complex specification of say, a C compiler.  Then proving Ï† in Agda is akin to
  -- explicitly constructing a C compiler that matches its specification Ï†.  Very nice (but quite a lot of
  -- work)!

  -- Now, back to existential quantification.  We previously noted that proving Ï† âˆ¨ Ïˆ in Agda requires an
  -- explicit construction of either Ï† or Ïˆ.  What about existential quantification?  In classical
  -- mathematics we can prove `âˆƒx. P x' by instead proving that, for every x, it is instead impossible for
  -- `P x' not to hold.  That is, we can prove `âˆƒx. P x' by instead proving `Â¬ âˆ€x. Â¬(P x)', its
  -- De Morgan dual.  But, if we are trying to explicitly build terms that inhabit types, this
  -- indirect form of proof is horrible!  We've established `âˆƒx. P x' but we cannot get our hands on any
  -- construction of `x' that satisfies `P'.
  --
  -- Constructive mathematics therefore interprets existential quantification differently from classical
  -- mathematics, to show `âˆƒx. P x' constructively we must explicitly construct an `x' that satisfies `P'.
  -- Proofs of `âˆƒx. P x' are therefore pairs consisting of an x and a proof.  In Agda, this notion is
  -- captured as a dependent pair, or Î£-type, where the second element of the pair `depends' on the first
  -- element.  The definition is below:

  data Î£ (A : Set) (P : A â†’ Set) : Set where
    _,_ : (x : A) â†’ P x â†’ Î£ A P

  -- Of course, like Î -types and the function space arrow â†’, our existing notion of Cartesian product type,
  -- _Ã—_ is really just a special case of Î£:

  _Ã—_ : Set â†’ Set â†’ Set
  A Ã— B = Î£ A (Î» x â†’ B)

  -- We can recover our previous definitions on _Ã—_ easily:

  fst : (A B : Set) â†’ A Ã— B â†’ A
  fst A B (x , y) = x

  snd : (A B : Set) â†’ A Ã— B â†’ B
  snd A B (x , y) = y

  -- And we therefore no longer need our old definition of _Ã—_ any more.
  --
  -- Let us see how to use Î£.  First, we'll make use of Agda's ability to infer types to make using Î£ a little
  -- bit nicer, so we don't have to clutter up our types any more than we strictly have to:

  âˆƒ : {A : Set} â†’ (A â†’ Set) â†’ Set
  âˆƒ {A} P = Î£ A P

  -- Now, let's again define the Booleans and some operations over them, as well as propositional equality:

  data Bool : Set where
    true false : Bool -- can put multiple constructors on the same line if they have the same type

  _âˆ§_ : Bool â†’ Bool â†’ Bool
  true  âˆ§ x = x
  false âˆ§ x = false

  data _â‰¡_ {A : Set} : A â†’ A â†’ Set where
    refl : {x : A} â†’ x â‰¡ x

  -- Let's use âˆƒ to prove something very trivial:

  silly-exampleâ‚ : (b : Bool) â†’ âˆƒ Î» bâ€² â†’ (b âˆ§ bâ€²) â‰¡ bâ€²
  silly-exampleâ‚ true = {!!}
  silly-exampleâ‚ false = {!!}

  -- Take a look at the goal state above.  We must inhabit `âˆƒ Î» bâ€² â†’ (b âˆ§ bâ€²) â‰¡ bâ€²' given `b : Bool'.
  -- We proceed by case analysis on b:

  silly-exampleâ‚‚ : (b : Bool) â†’ âˆƒ Î» bâ€² â†’ (b âˆ§ bâ€²) â‰¡ bâ€²
  silly-exampleâ‚‚ true  = {!!}
  silly-exampleâ‚‚ false = {!!}

  -- The first hole requires us to construct some bâ€² such that bâ€² â‰¡ bâ€².  But any bâ€² will do here, as
  -- everything is equal to itself via `refl'.  The second hole requires us to exhibit some bâ€² such
  -- that false â‰¡ bâ€².  It does not take a genius to conclude that bâ€² must therefore be false.  We
  -- can refine our two holes further, using <Ctrl> + <c> + <r> to obtain:

  silly-exampleâ‚ƒ : (b : Bool) â†’ âˆƒ Î» bâ€² â†’ (b âˆ§ bâ€²) â‰¡ bâ€²
  silly-exampleâ‚ƒ true  = {!!} , {!!}
  silly-exampleâ‚ƒ false = {!!} , {!!}

  -- As an inhabitant of âˆƒ is a pair, as previously discussed.  Let's fill in our guesses for the first
  -- components of each pair:

  silly-exampleâ‚„ : (b : Bool) â†’ âˆƒ Î» bâ€² â†’ (b âˆ§ bâ€²) â‰¡ bâ€²
  silly-exampleâ‚„ true  = true  , {!!}
  silly-exampleâ‚„ false = false , {!!}

  -- Look at the types of the holes again.  In the first we have `true â‰¡ true' and in the second we have
  -- `false â‰¡ false'.  Both of these holes can therefore be replaced with refl, to obtain:

  silly-example : (b : Bool) â†’ âˆƒ Î» bâ€² â†’ (b âˆ§ bâ€²) â‰¡ bâ€²
  silly-example true  = true  , refl
  silly-example false = false , refl

  -- And we are done!  In summary, to inhabit `âˆƒ Î» x â†’ P x' we must explicitly construct a pair `x , p'
  -- where `x' satisfies `P', as witnessed by the second component `p' (of type `P x').

  -- EXERCISE: complete the following:

  âˆƒ-exerciseâ‚ : âˆƒ Î» b â†’ (b âˆ§ b) â‰¡ b
  âˆƒ-exerciseâ‚ = false , refl

  -- we can talk about the existence of functions, too!
  âˆƒ-exerciseâ‚‚ : (b : Bool) â†’ âˆƒ Î» (f : Bool â†’ Bool â†’ Bool) â†’ f b b â‰¡ b
  âˆƒ-exerciseâ‚‚ b = (Î» x y â†’ x) , refl
